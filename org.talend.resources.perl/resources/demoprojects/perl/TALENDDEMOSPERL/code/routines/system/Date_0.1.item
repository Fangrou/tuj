use Exporter;
use Time::Local;

use vars qw(@EXPORT @ISA);
@ISA = qw(Exporter);
@EXPORT = qw(
    getDate
    getRandomDate
    datestringISOtoFr
    datestringISOtoUS
);


##
# return the current datetime with the given display format
#
# {Category} Date
# {talendTypes} date
#
# {param} string('CCYY-MM-DD') format : string representing the wished format of the date
#
# {example} getCurrentDate('CCYY-MM-DD')
# {example} getCurrentDate('DD/MM/CCYY')
# {example} getCurrentDate(undef)
sub getDate {
    my ($format, $time) = @_;
    $format = 'DD/MM/CCYY' if not defined $format;
    $time = time() if not defined $time;

    my ($sec,$min,$hour,$mday,$mon,$year,$wday,$yday,$isdst) =
        localtime($time);

    my %fields = (
        CC => int(($year + 1900) / 100),
        YY => $year % 100,
        MM => $mon + 1,
        DD => $mday,
        hh => $hour,
        mm => $min,
        ss => $sec
    );

    %fields = map {$_ => sprintf('%02u', $fields{$_})} keys %fields;

    foreach my $field (keys %fields) {
        $format =~ s/$field/$fields{$field}/g;
    }

    return $format;
}

##
# return an ISO formatted random date
#
# {Category} Date
# {talendTypes} date
#
# {param} string('2007-01-01') min : minimum date
# {param} string('2008-12-31') max : maximum date (superior to min)
#
# {example} getRandomDate('1981-01-18', '2005-07-24')
# {example} getRandomDate('1980-12-08', '2007-02-26')
sub getRandomDate {
    my %params;
    @params{min, max} = @_;

    my ($year, $month, $day);

    my $regex = '(\d{4})-(\d{2})-(\d{2})';

    ($year, $month, $day) = ($params{min} =~ m{$regex});
    my $min = timelocal(0, 0, 0, $day, $month-1, $year);

    ($year, $month, $day) = ($params{max} =~ m{$regex});
    my $max = timelocal(0, 0, 0, $day, $month-1, $year);

    my $diff = $max - $min;

    {
        my ($sec,$min,$hour,$mday,$mon,$year,$wday,$yday,$isdst) =
            localtime($min + int rand $diff);

        return sprintf(
            '%4u-%02u-%02u',
            $year + 1900,
            $mon + 1,
            $mday,
        );
    }
}

sub datestringISOtoFr {
    my ($datestring) = @_;

    my ($year, $month, $day) = ($datestring =~ m/(\d{4})-(\d{2})-(\d{2})/);

    return $day.'/'.$month.'/'.$year;
}

sub datestringISOtoUS {
    my ($datestring) = @_;

    my ($year, $month, $day) = ($datestring =~ m/(\d{4})-(\d{2})-(\d{2})/);

    return $month.'/'.$day.'/'.$year;
}

1;

